buildscript {
  dependencies {
    // https://github.com/fabric8io-images/run-java-sh
    // run-java.sh 是Java应用程序的通用启动脚本，特别是为从容器中运行而精心编制的。
    classpath("io.fabric8:run-java-sh:1.3.8")
  }
}

plugins {
  id "io.spring.dependency-management" version "1.0.9.RELEASE"
  id "com.github.johnrengelman.shadow" version "5.2.0"
  id "com.github.hierynomus.license" version "0.15.0"
  id "me.champeau.gradle.jmh" version "0.5.0"
  id "org.flywaydb.flyway" version "7.3.1"
  id "com.novoda.build-properties" version "0.4.1"
  id "org.hidetake.ssh" version "2.10.1"
}

allprojects {

  apply plugin: "java"
  apply plugin: "java-library"
  apply plugin: "jacoco"
  apply plugin: "checkstyle"
  apply plugin: "io.spring.dependency-management"
  apply plugin: "com.github.johnrengelman.shadow"
  apply plugin: "me.champeau.gradle.jmh"

  sourceCompatibility = "11"
  targetCompatibility = "11"

  [compileJava, compileTestJava, javadoc]*.options*.encoding = "UTF-8"

  test {
    useJUnitPlatform()
  }

  jacocoTestReport {
    reports {
      xml.enabled = true
      html.enabled = true
    }
  }

  license {
    header = rootProject.file("LICENSE")
    headerURI = com.hierynomus.gradle.license.LicenseBasePlugin.classLoader.getResource("headers/Apache-2.0").toURI()
    skipExistingHeaders = true
    mapping {
      java = "SLASHSTAR_STYLE"
    }
    ext {
      year = "2019-2020"
      author = "the original author or authors"
    }
  }

  checkstyle {
    toolVersion = "8.33"
    configFile = project.file("config/checkstyle/google_checks.xml")
  }

  tasks.withType(Checkstyle) {
    reports {
      xml.enabled = true
      html.enabled = true
    }
  }

  dependencyManagement {
    imports {
      mavenBom("top.yein.chaos:chaos-bom:1.0-SNAPSHOT")
    }
  }

  dependencies {
    compileOnly("org.projectlombok:lombok")
    annotationProcessor("org.projectlombok:lombok")
    compileOnly("com.google.code.findbugs:jsr305")
    api("org.apache.logging.log4j:log4j-api")

//    implementation("org.apache.logging.log4j:log4j-core")
//    implementation("org.apache.logging.log4j:log4j-slf4j-impl")

    // Test
    testCompileOnly("org.projectlombok:lombok")
    testAnnotationProcessor("org.projectlombok:lombok")
    testImplementation("org.openjdk.jol:jol-core")
    testImplementation("org.junit.platform:junit-platform-launcher")
    testImplementation("org.junit.jupiter:junit-jupiter-engine")
    testImplementation("com.github.stefanbirkner:system-lambda")
    testImplementation("io.projectreactor:reactor-test")
    testImplementation("io.micrometer:micrometer-test")
    testImplementation("org.assertj:assertj-core")
    testImplementation("org.mockito:mockito-core")
    testImplementation("org.mockito:mockito-inline")
    testImplementation("org.powermock:powermock-reflect")
    testImplementation("org.testcontainers:testcontainers")
    testImplementation("org.testcontainers:junit-jupiter")

    jmh("org.openjdk.jmh:jmh-core")
    jmh("org.openjdk.jmh:jmh-generator-annprocess")
  }

  repositories {
    mavenLocal()

    def aliyunEnabled = System.getenv("GITHUB_ACTIONS") == null
    if (aliyunEnabled) {
      maven { url = "https://maven.aliyun.com/nexus/content/groups/public/" }
    }
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots" }
    mavenCentral()
  }
}